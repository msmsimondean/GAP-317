plugins {
    id "com.jfrog.artifactory" version "4.15.2" apply false
}

apply plugin: "java-library"
apply plugin: "maven"
apply plugin: 'maven-publish'
apply plugin: "com.jfrog.artifactory"

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

javadoc {
    failOnError false
    source = sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

artifactory {
    contextUrl = 'https://artifactory.example.com/artifactory'
    resolve {
        repoKey = 'example'
        repository {
            maven = true
        }
    }

    publish {
        repository {
            repoKey = 'example'
            username = 'example-username'
            password = 'example-password'
        }
        defaults {
            publications('mavenPublication')
        }
    }
    clientConfig.info.setBuildNumber(project.hasProperty('buildNumber') ? project.property('buildNumber') : System.getenv('BUILD_NUMBER'))
}

task printHttpClientJarFile {
    doLast {
        Class klass = org.apache.http.impl.client.HttpClientBuilder.class

        print klass.getResource("/${klass.name.replace(".", "/")}.class")
    }
}
